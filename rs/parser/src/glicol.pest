block = {SOI~ sentence+ ~EOI}
sentence = {(track_name|reference) ~ ":" ~ chain}
track_name = {valid_ident}
chain = {node ~ ( ">>" ~ node)*}
node = { sin | mul | seq | lpf | reverb | sampler | receiver | vocoder | sonic}

receiver = { reference }

// meta node

// sampler
sampler = { "sampler" ~ sample_name ~ (trigger_num | sampler_config?) }
sample_name = {symbol}
sampler_config = {"{" ~ trigger_num? ~ start? ~ end? ~ attack? ~ decay? ~ ps? ~ ts? ~ "}"}
trigger_num = {","? ~ "trigger_num"? ~ ":"?~number ~ ","?}
start = {","? ~ "start"~":"~number ~ ","?}
end = {","? ~ "end"~":"~number ~ ","?}
attack = {","? ~ "attack"~":"~number ~ ","?}
decay = {","? ~ "decay"~":"~number ~ ","?}
ps = {","? ~ "ps"~":"~number ~ ","?}
ts = {","? ~ "ts"~":"~number ~ ","?}

// compound node
reverb = { "reverb" ~ reverb_config }

reverb_config = {"{" ~ ("mix"~":"~number) ~ (","~"bw"~":"~number)?  ~ (","~"damping"~":"~number)?  ~ (","~"decay"~":"~number)?  ~ "}" }

// env

// seq takes optional hypeparams, midi and rest
seq = { "seq" ~ notes+ ~ seq_config? }
seq_config = {"{" ~ seq_span ~ "}"}
seq_span = { "span" ~ ":" ~ number }
notes = ${ (midi|rest|note_ref)+ }
midi = {ASCII_DIGIT+}
rest = {"_"} // this looks funny
note_ref = { "~" ~ ASCII_ALPHA_LOWER }

// sin osc
sin = { "sin" ~ sin_args ~ sin_config?  }
sin_config = {"{" ~ sin_phase ~ "}"}
sin_phase = { "phase" ~ ":" ~ number }
sin_args = { freq | reference | sin_multichan }
sin_multichan = { "["~ (freq|reference)+ ~ "]"}
freq = {number}

// effects
vocoder = {("pv" | "vocoder") ~ pitch_shift ~ time_stretch}
pitch_shift = { number }
time_stretch = { number }

// effects
sonic = {"sonic" ~ speed ~ pitch}
speed = { number }
pitch = { number }

// filters
lpf = {("lpf" | "rlpf") ~ cutoff ~ q}
hpf = {("hpf" | "rhpf") ~ cutoff ~ q}
cutoff = { number }
q = { number }

// signal nodes

// op nodes
mul = {"mul" ~ (number | reference)}
number = @{ASCII_DIGIT+ ~ "."? ~ ASCII_DIGIT* }

reference = @{"~" ~ valid_ident }
symbol = @{"\\" ~ valid_ident }
valid_ident = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER|"_"|ASCII_DIGIT)* }

keywords = {"sin"|"mul"|"lpf"|"seq"|"sp"|"sampler"|"reverb"|"vocoder"|"pv"|"sonic"}

COMMENT = _{ ("/-" ~ (!"-/" ~ ANY)* ~ "-/" ) | ("//" ~ (!"\n" ~ ANY)* ) }
WHITESPACE = _{" " | "\t" | "\n"}